import com.android.builder.internal.ClassFieldImpl
import groovy.json.JsonSlurper

group 'com.apphud.app'
version '1.0-SNAPSHOT'

String apphudFlutterSdkVersion = 'unknown'

try {
    String packagesContent = new File('../.dart_tool/package_config.json').text
    def jsonSlurper = new JsonSlurper()
    def packagesMap = jsonSlurper.parseText(packagesContent)
    def packagesList = packagesMap.packages
    assert packagesList instanceof List

    def rootUri = packagesList.findResult { it.name == 'apphud' ? it.rootUri : null }
    assert rootUri != null

    def path = rootUri.replace('file://', '') + '/pubspec.yaml'
    String pubspecContent = new File(path).text

    def pubspecList = pubspecContent.split('\n')
    for (String pubspecItem in pubspecList) {
        String[] lineItems = pubspecItem.split(':')
        if (lineItems.length > 1 && lineItems[0].trim() == 'version') {
            apphudFlutterSdkVersion = lineItems[1].trim();
            break
        }
    }
} catch (Error e) {
    apphudFlutterSdkVersion = 'unknown'
}

buildscript {
    ext.kotlin_version = '1.5.0'
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16
        it.addBuildConfigField(new ClassFieldImpl('String', 'APPHUD_FLUTTER_SDK_VERSION', '\"' + apphudFlutterSdkVersion + '\"'))
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.apphud:ApphudSDK-Android:1.6.4"
    implementation 'com.android.billingclient:billing:4.0.0'
    implementation 'com.google.code.gson:gson:2.8.8'
}
